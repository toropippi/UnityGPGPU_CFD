//表示用のRenderTextureにはVorticityで渦度を計算するのと一緒に書き込んでいることに注意
#pragma kernel Newgrad_u
#pragma kernel Newgrad_v
#pragma kernel Upwind_u
#pragma kernel Upwind_v
#pragma kernel Cip_u
#pragma kernel Cip_v
#pragma kernel Pressure0
#pragma kernel Pressure1
#pragma kernel Div
#pragma kernel Rhs
#pragma kernel Veloc
#pragma kernel Visc
#pragma kernel Vorticity
#pragma kernel ComputeBufferMemcopy_i
#pragma kernel ComputeBufferMemcopy_f
#define WX (uint)(192)
#define WY (uint)(144)
#define SPEEDLIMIT 0.95f

float ALPHA;//(float)1.79
float DT;//delta time
float MU;

uint SIZE;//データコピーの際に使用する
uint OFFSETDST;//データコピーの際に使用する
uint OFFSETSRC;//データコピーの際に使用する

RWStructuredBuffer<float> u;
RWStructuredBuffer<float> v;
RWStructuredBuffer<float> fn;
RWStructuredBuffer<float> gxn;
RWStructuredBuffer<float> gyn;
RWStructuredBuffer<float> GXd;
RWStructuredBuffer<float> GYd;
RWStructuredBuffer<float> yn;
RWStructuredBuffer<float> y;
RWStructuredBuffer<float> GX;
RWStructuredBuffer<float> GY;
RWStructuredBuffer<float> YU;
RWStructuredBuffer<float> YUN;
RWStructuredBuffer<float> YUV;
RWStructuredBuffer<float> YV;
RWStructuredBuffer<float> YVN;
RWStructuredBuffer<float> GXU;
RWStructuredBuffer<float> GYU;
RWStructuredBuffer<float> GXV;
RWStructuredBuffer<float> GYV;
RWStructuredBuffer<float> YVU;
RWStructuredBuffer<float> YP;
RWStructuredBuffer<float> YPN;
RWStructuredBuffer<float> DIV;
RWStructuredBuffer<float> VOR;
RWStructuredBuffer<uint> Wall;
RWStructuredBuffer<uint> WallP;
RWStructuredBuffer<uint> WallX;
RWStructuredBuffer<uint> WallY;

RWStructuredBuffer<uint> DATADSTI;
RWStructuredBuffer<uint> DATASRCI;
RWStructuredBuffer<float> DATADSTF;
RWStructuredBuffer<float> DATASRCF;

RWTexture2D<float4> Tex;

























// (192, 1, 1)のスレッドで回す
[numthreads(WX, 1, 1)]
void Newgrad_u(int2 id : SV_DispatchThreadID) 
{
	int i = id.x;
	int j = id.y;
	int i0 = (i - 1 + WX) % WX;
	int i1 = (i + 1) % WX;
	int j0 = (j - 1 + WY) % WY;
	int j1 = (j + 1) % WY;
	j *= WX;
	j0 *= WX;
	j1 *= WX;
	int ij = i + j;
	GX[ij] += (yn[i1 + j] - yn[i0 + j] - y[i1 + j] + y[i0 + j]) * 0.5f * (Wall[ij] > 128);
	GY[ij] += (yn[i + j1] - yn[i + j0] - y[i + j1] + y[i + j0]) * 0.5f * (Wall[ij] > 128);
}
// (192, 1, 1)のスレッドで回す
[numthreads(WX, 1, 1)]
void Newgrad_v(int2 id : SV_DispatchThreadID)
{
	int i = id.x;
	int j = id.y;
	int i0 = (i - 1 + WX) % WX;
	int i1 = (i + 1) % WX;
	int j0 = (j - 1 + WY) % WY;
	int j1 = (j + 1) % WY;
	j *= WX;
	j0 *= WX;
	j1 *= WX;
	int ij = i + j;
	GX[ij] += (yn[i1 + j] - yn[i0 + j] - y[i1 + j] + y[i0 + j]) * 0.5f * (Wall[ij] > 128);
	GY[ij] += (yn[i + j1] - yn[i + j0] - y[i + j1] + y[i + j0]) * 0.5f * (Wall[ij] > 128);
}




//一次風上差分のx成分
[numthreads(WX, 1, 1)]
void Upwind_u(int2 id : SV_DispatchThreadID) {
	int i = id.x;
	int j = id.y;
	int i0 = (i - 1 + WX) % WX;
	int i1 = (i + 1) % WX;
	int j0 = (j - 1 + WY) % WY;
	int j1 = (j + 1) % WY;
	j *= WX;
	j0 *= WX;
	j1 *= WX;
	int ij = i + j;
	float u = YU[ij];
	float v = YVU[ij];//YUVではありません。y速度の平均が必要です。
	if (Wall[ij] > 128) {
		if ((u >= 0.0)&(v >= 0.0)) {
			YUN[ij] = YU[ij] - u * (YU[ij] - YU[i0 + j])*DT - v * (YU[ij] - YU[i + j0])*DT;
		}

		if ((u < 0.0)&(v >= 0.0)) {
			YUN[ij] = YU[ij] - u * (YU[i1 + j] - YU[ij])*DT - v * (YU[ij] - YU[i + j0])*DT;
		}

		if ((u >= 0.0)&(v < 0.0)) {
			YUN[ij] = YU[ij] - u * (YU[ij] - YU[i0 + j])*DT - v * (YU[i + j1] - YU[ij])*DT;
		}

		if ((u < 0.0)&(v < 0.0)) {
			YUN[ij] = YU[ij] - u * (YU[i1 + j] - YU[ij])*DT - v * (YU[i + j1] - YU[ij])*DT;
		}
	}

}

//一次風上差分のy成分
[numthreads(WX, 1, 1)]
void Upwind_v(int2 id : SV_DispatchThreadID) {
	int i = id.x;
	int j = id.y;
	int i0 = (i - 1 + WX) % WX;
	int i1 = (i + 1) % WX;
	int j0 = (j - 1 + WY) % WY;
	int j1 = (j + 1) % WY;
	j *= WX;
	j0 *= WX;
	j1 *= WX;
	int ij = i + j;

	float u = YUV[ij];//YVUではありません。x速度の平均が必要です。
	float v = YV[ij];//
	if (Wall[ij] > 128) {
		if ((u >= 0.0)&(v >= 0.0)) {
			YVN[ij] = YV[ij] - u * (YV[ij] - YV[i0 + j])*DT - v * (YV[ij] - YV[i + j0])*DT;
		}

		if ((u < 0.0)&(v >= 0.0)) {
			YVN[ij] = YV[ij] - u * (YV[i1 + j] - YV[ij])*DT - v * (YV[ij] - YV[i + j0])*DT;
		}

		if ((u >= 0.0)&(v < 0.0)) {
			YVN[ij] = YV[ij] - u * (YV[ij] - YV[i0 + j])*DT - v * (YV[i + j1] - YV[ij])*DT;
		}

		if ((u < 0.0)&(v < 0.0)) {
			YVN[ij] = YV[ij] - u * (YV[i1 + j] - YV[ij])*DT - v * (YV[i + j1] - YV[ij])*DT;
		}
	}

}






//cip法x方向計算
[numthreads(WX, 1, 1)]
void Cip_u(int2 id : SV_DispatchThreadID) {
	int i = id.x;
	int j = id.y;
	int jwx = j*WX;
	float a1;
	float b1;
	float c1;
	float d1;
	float f1;
	float e1;
	float g1;
	float tmp0;

	float ui0 = u[(i + WX - 1) % WX + jwx];
	float u_jwx = u[i+jwx];
	float ui1 = u[(i + 1) % WX + jwx];
	float vi0 = v[(i + WX - 1) % WX + jwx];
	float yy = -v[i + jwx] * DT;
	float vi1 = v[(i + 1) % WX + jwx];
	float xx = -u_jwx * DT;

	int isn;
	int jsn;
	float u_im1;// = u[im1];

	if (xx == 0.0f) { isn = 0; u_im1 = u_jwx; }
	else {
		if (xx>0.0f) { isn = -1; u_im1 = ui1;}
		else { isn = 1; u_im1 = ui0;}
	}
	if (yy == 0.0f) { jsn = 0; }
	else {
		if (yy>0.0f) { jsn = -1; }
		else { jsn = 1; }
	}

	int im1 = (i - isn + WX) % WX;
	int jm1 = ((j - jsn + WY) % WY)*WX;
	int jj = i + jm1;//ｙの風上
	jm1 += im1;//ｘｙの風上
	im1 += jwx;//ｘの風上
	jwx += i;//風上なし

	float gxd_jwx = GXd[jwx];
	float gyd_jwx = GYd[jwx];
	float u_j = u[jj];
	float gxd_im1 = GXd[im1];
	float gyd_j = GYd[jj];
	a1 = u_jwx - u_j - u_im1 + u[jm1];
	b1 = GYd[im1] - gyd_jwx;
	d1 = (-a1 - b1*jsn)*isn;
	c1 = (-a1 - (GXd[jj] - gxd_jwx)*isn)*jsn;
	g1 = (c1 - b1)*isn;
	a1 = gxd_im1 + gxd_jwx - 2.0f*isn*(u_jwx - u_im1);
	b1 = gyd_j + gyd_jwx - 2.0f*jsn*(u_jwx - u_j);
	e1 = 3.0f*(u_im1 - u_jwx) + (gxd_im1 + 2.0f*gxd_jwx)*isn;
	f1 = 3.0f*(u_j - u_jwx) + (gyd_j + 2.0f*gyd_jwx)*jsn;
	a1 *= xx;
	b1 *= yy;

	int wall = Wall[jwx];//壁の辺なら速度の更新を行わない
	if (wall >128) {//流体なら。x速度の値を更新
		tmp0 = ((a1 + c1*yy + e1)*xx + g1*yy + gxd_jwx)*xx + ((b1 + d1*xx + f1)*yy + gyd_jwx)*yy + u_jwx;
		if (tmp0<-SPEEDLIMIT) { tmp0 = -SPEEDLIMIT; }//速度クランプ間違って発散しても破綻しないように
		if (tmp0> SPEEDLIMIT) { tmp0 =  SPEEDLIMIT; }//速度クランプ
		fn[jwx] = tmp0;
	}
	a1 = (3.0f*a1 + 2.0f*(c1*yy + e1))*xx + (d1*yy + g1)*yy + gxd_jwx;
	b1 = (3.0f*b1 + 2.0f*(d1*xx + f1))*yy + (c1*xx + g1)*xx + gyd_jwx;

	im1 = (i + WX - 1) % WX + j*WX;
	jm1 = (i + 1) % WX + j*WX;

	if (wall >128) {//流体なら。x速度をx方向に微分した値を更新
		tmp0 = (a1 - 0.5f*DT*(a1*(ui1 - ui0) + b1*(vi1 - vi0)));
		if (tmp0<-SPEEDLIMIT) { tmp0 = -SPEEDLIMIT; }
		if (tmp0> SPEEDLIMIT) { tmp0 = SPEEDLIMIT; }
		gxn[jwx] = tmp0;
	}

	im1 = ((j + WY - 1) % WY)*WX + i;
	jm1 = ((j + 1) % WY)*WX + i;
	float uj0;
	float uj1;
	if (isn == 0) { uj0 = u[im1]; uj1 = u[jm1]; }
	else {
		if (isn == 1) { uj0 = u_j; uj1 = u[jm1]; }
		else { uj0 = u[im1]; uj1 = u_j; }
	}
	if (wall >128) {//流体なら。x速度をy方向に微分した値を更新
		tmp0 = (b1 - 0.5f*DT*(a1*(uj1 - uj0) + b1*(v[jm1] - v[im1])));
		if (tmp0<-SPEEDLIMIT) { tmp0 = -SPEEDLIMIT; }
		if (tmp0> SPEEDLIMIT) { tmp0 =  SPEEDLIMIT; }
		gyn[jwx] = tmp0;
	}
}



//cip法のy方向計算
[numthreads(WX, 1, 1)]
void Cip_v(int2 id : SV_DispatchThreadID) {
	int i = id.x;
	int j = id.y;
	int jwx = j*WX;
	float a1;
	float b1;
	float c1;
	float d1;
	float f1;
	float e1;
	float g1;
	float tmp0;

	float vi0 = v[(i + WX - 1) % WX + jwx];
	float v_jwx = v[i+jwx];
	float vi1 = v[(i + 1) % WX + jwx];
	float ui0 = u[(i + WX - 1) % WX + jwx];
	float xx = -u[i + jwx] * DT;
	float ui1 = u[(i + 1) % WX + jwx];
	float yy = -v_jwx * DT;

	int isn;//i sign
	int jsn;//j sign
	float v_im1;// = v[im1];

	if (xx == 0.0f) { isn = 0; v_im1 = v_jwx; }
	else {
		if (xx>0.0f) { isn = -1; v_im1 = vi1;}
		else { isn = 1; v_im1 = vi0;}
	}
	if (yy == 0.0f) { jsn = 0; }
	else {
		if (yy>0.0f) { jsn = -1; }
		else { jsn = 1; }
	}

	int im1 = (i - isn + WX) % WX;
	int jm1 = ((j - jsn + WY) % WY)*WX;
	int jj = i + jm1;//ｙの風上
	jm1 += im1;//ｘｙの風上
	im1 += jwx;//ｘの風上
	jwx += i;//風上なし

	float gxd_jwx = GXd[jwx];
	float gyd_jwx = GYd[jwx];
	float v_j = v[jj];
	float gxd_im1 = GXd[im1];
	float gyd_j = GYd[jj];
	a1 = v_jwx - v_j - v_im1 + v[jm1];
	b1 = GYd[im1] - gyd_jwx;
	d1 = (-a1 - b1*jsn)*isn;
	c1 = (-a1 - (GXd[jj] - gxd_jwx)*isn)*jsn;
	g1 = (c1 - b1)*isn;
	a1 = gxd_im1 + gxd_jwx - 2.0f*isn*(v_jwx - v_im1);
	b1 = gyd_j + gyd_jwx - 2.0f*jsn*(v_jwx - v_j);
	e1 = 3.0f*(v_im1 - v_jwx) + (gxd_im1 + 2.0f*gxd_jwx)*isn;
	f1 = 3.0f*(v_j - v_jwx) + (gyd_j + 2.0f*gyd_jwx)*jsn;
	a1 *= xx;
	b1 *= yy;

	int wall = Wall[jwx];//壁の辺なら速度の更新を行わない
	if (wall >128) {//流体なら。y速度の値を更新
		tmp0 = ((a1 + c1*yy + e1)*xx + g1*yy + gxd_jwx)*xx + ((b1 + d1*xx + f1)*yy + gyd_jwx)*yy + v_jwx;
		if (tmp0<-SPEEDLIMIT) { tmp0 = -SPEEDLIMIT; }
		if (tmp0> SPEEDLIMIT) { tmp0 =  SPEEDLIMIT; }
		fn[jwx] = tmp0;
	}
	a1 = (3.0f*a1 + 2.0f*(c1*yy + e1))*xx + (d1*yy + g1)*yy + gxd_jwx;
	b1 = (3.0f*b1 + 2.0f*(d1*xx + f1))*yy + (c1*xx + g1)*xx + gyd_jwx;

	if (wall >128) {//流体なら。y速度をx方向に微分した値を更新
		tmp0 = (a1 - 0.5f*DT*(a1*(ui1 - ui0) + b1*(vi1 - vi0)));
		if (tmp0<-SPEEDLIMIT) { tmp0 = -SPEEDLIMIT; }
		if (tmp0> SPEEDLIMIT) { tmp0 =  SPEEDLIMIT; }
		gxn[jwx] = tmp0;
	}

	im1 = ((j + WY - 1) % WY)*WX + i;
	jm1 = ((j + 1) % WY)*WX + i;
	float vj0;
	float vj1;
	if (jsn == 0) {vj0 = v[im1];vj1 = v[jm1];}
	else {
		if (jsn == 1) { vj0 = v_j; vj1 = v[jm1]; }
		else{ vj0 = v[im1]; vj1 = v_j; }
	}
	if (wall >128) {//流体なら。y速度をy方向に微分した値を更新
		tmp0 = (b1 - 0.5f*DT*(a1*(u[jm1] - u[im1]) + b1*(vj1 - vj0)));
		if (tmp0<-SPEEDLIMIT) { tmp0 = -SPEEDLIMIT; }
		if (tmp0> SPEEDLIMIT) { tmp0 = SPEEDLIMIT; }
		gyn[jwx] = tmp0;
	}
}













// (128, 1, 1)のスレッドで回す
[numthreads(128, 1, 1)]
void Pressure0(uint id : SV_DispatchThreadID) {
	uint i = id*2+1;
	uint j = i / WX;
	i -= j * WX + ((j % 2) == 1);//違いはここだけ
	//この時点でi,jは確定
	uint i0 = j * WX + (i + WX - 1) % WX;
	uint i1 = j * WX + (i      + 1) % WX;
	uint j0 = ((j + WY - 1) % WY)*WX + i;
	uint j1 = ((j      + 1) % WY)*WX + i;
	uint ij = j * WX + i;
	float ff = DIV[ij];
	float p = YPN[ij];
	float f1 = YPN[i0];
	float f2 = YPN[i1];
	float f3 = YPN[j0];
	float f4 = YPN[j1];

	if (WallP[i0]>64) { ff -= f1; }
	else { ff -= p; }
	if (WallP[i1]>64) { ff -= f2; }
	else { ff -= p; }
	if (WallP[j0]>64) { ff -= f3; }
	else { ff -= p; }
	if (WallP[j1]>64) { ff -= f4; }
	else { ff -= p; }

	YPN[ij] -= (0.25f*ff+p)*ALPHA*(WallP[ij]>128);
}


// (128, 1, 1)のスレッドで回す
[numthreads(128, 1, 1)]
void Pressure1(uint id : SV_DispatchThreadID) {
	uint i = id * 2 + 1;
	uint j = i / WX;
	i -= j * WX + ((j % 2) == 0);//違いはここだけ
								 //この時点でi,jは確定
	uint i0 = j * WX + (i + WX - 1) % WX;
	uint i1 = j * WX + (i + 1) % WX;
	uint j0 = ((j + WY - 1) % WY)*WX + i;
	uint j1 = ((j + 1) % WY)*WX + i;
	uint ij = j * WX + i;
	float ff = DIV[ij];
	float p = YPN[ij];
	float f1 = YPN[i0];
	float f2 = YPN[i1];
	float f3 = YPN[j0];
	float f4 = YPN[j1];

	if (WallP[i0]>64) { ff -= f1; }
	else { ff -= p; }
	if (WallP[i1]>64) { ff -= f2; }
	else { ff -= p; }
	if (WallP[j0]>64) { ff -= f3; }
	else { ff -= p; }
	if (WallP[j1]>64) { ff -= f4; }
	else { ff -= p; }

	YPN[ij] -= (0.25f*ff + p)*ALPHA*(WallP[ij]>128);
}



[numthreads(WX, 1, 1)]
void Div(int2 id : SV_DispatchThreadID) {
	int i = id.x;
	int j = id.y;
	int i1 = (i + 1) % WX;
	int j1 = (j + 1) % WY;
	j *= WX;
	j1 *= WX;
	int ij = i + j;
	DIV[ij] = (YUN[i1 + j] - YUN[ij] + YVN[i + j1] - YVN[ij]);
}




[numthreads(WX, 1, 1)]
void Rhs(int2 id : SV_DispatchThreadID) {
	int i = id.x;
	int j = id.y;
	int i0 = (i - 1 + WX) % WX;
	int j0 = (j - 1 + WY) % WY;
	j *= WX;
	j0 *= WX;
	int ij = i + j;
	YUN[ij] -= (YPN[ij] - YPN[i0 + j])*(WallX[ij]>128);
	YVN[ij] -= (YPN[ij] - YPN[i + j0])*(WallY[ij]>128);
}


//cip法で使う中間速度定義点をここで計算している
[numthreads(WX, 1, 1)]
void Veloc(int2 id : SV_DispatchThreadID) {
	int i = id.x;
	int j = id.y;
	int i0 = (i - 1 + WX) % WX;
	int i1 = (i + 1) % WX;
	int j0 = (j - 1 + WY) % WY;
	int j1 = (j + 1) % WY;
	j *= WX;
	j0 *= WX;
	j1 *= WX;
	int ij = i + j;
	float tv = YVN[ij] + YVN[i + j1];
	float tu = YUN[ij] + YUN[i1 + j];
	YVU[ij] = 0.25f*(tv + YVN[i0 + j] + YVN[i0 + j1]);
	YUV[ij] = 0.25f*(tu + YUN[i + j0] + YUN[i1 + j0]);
}


[numthreads(WX, 1, 1)]
void Visc(int2 id : SV_DispatchThreadID) {
	int i = id.x;
	int j = id.y;
	int i0 = (i - 1 + WX) % WX;
	int i1 = (i + 1) % WX;
	int j0 = (j - 1 + WY) % WY;
	int j1 = (j + 1) % WY;
	j *= WX;
	j0 *= WX;
	j1 *= WX;

	if (WallX[i + j]>128) { 
		YUN[i + j] = YU[i + j] - (YU[i0 + j] + YU[i1 + j] + YU[i + j0] + YU[i + j1]) * 0.25f * DT * MU;
	}
	if (WallY[i + j]>128) { 
		YVN[i + j] = YV[i + j] - (YV[i0 + j] + YV[i1 + j] + YV[i + j0] + YV[i + j1]) * 0.25f * DT * MU;
	}
}






//渦度
[numthreads(WX, 1, 1)]
void Vorticity(int2 id : SV_DispatchThreadID)
{
	int i = id.x;
	int j = id.y;
	int i0 = (i - 1 + WX) % WX;
	int i1 = (i + 1) % WX;
	int j0 = (j - 1 + WY) % WY;
	int j1 = (j + 1) % WY;
	j *= WX;
	j0 *= WX;
	j1 *= WX;
	float uu = YUN[i + j0];
	float ud = YUN[i + j];
	float vl = YVN[i0 + j];
	float vr = YVN[i + j];
	//右回りを正とする
	float vor = uu - ud + vr - vl;
	VOR[i + j] = vor;



	vor = vor * 9.0;
	float4 col;
	col.x = clamp(-vor, 0, 1);
	col.y = clamp(vor, 0, 1);
	col.z = clamp(0.4 * (abs(vor) - 0.3), 0, 1);
	col.w = 1;

	Tex[id.xy] = col;
}





//配列コピーint版、完全コピー用
[numthreads(64, 1, 1)]
void ComputeBufferMemcopy_i(uint id : SV_DispatchThreadID)
{
	if (id < SIZE) {
		DATADSTI[id + OFFSETDST] = DATASRCI[id + OFFSETSRC];
	}
}


//配列コピーfloat版、完全コピー用
[numthreads(64, 1, 1)]
void ComputeBufferMemcopy_f(uint id : SV_DispatchThreadID)
{
	if (id < SIZE) {
		DATADSTF[id+ OFFSETDST] = DATASRCF[id+ OFFSETSRC];
	}
}
